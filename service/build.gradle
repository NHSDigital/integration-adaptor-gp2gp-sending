plugins {
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id "checkstyle"
	id "com.github.spotbugs" version "6.4.2"
	id "io.freefair.lombok" version "9.1.0"
	id 'jacoco'
	id 'org.sonarqube' version '6.3.1.5724'

	// Mutation testing
	id 'info.solidsoft.pitest' version '1.15.0'
	id 'com.arcmutate.github' version '2.2.4'
}

spotbugs {
	toolVersion = '4.9.5'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: "com.github.spotbugs"
apply plugin: 'application'

application {
	mainClassName = 'uk.nhs.adaptors.gp2gp.Gp2gpApplication'
}

group = 'uk.nhs.adaptors'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

checkstyle {
	toolVersion '10.18.2'
	configDirectory = 'checkstyle' as File
}

repositories {
	mavenCentral()
}

ext {
	set("springCloudVersion", "2025.0.0")
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
//	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework:spring-jms'

	// Use version specified in spring-boot-starter-web
	implementation 'com.fasterxml.jackson.core:jackson-databind'

	// WebClient
	implementation ('org.springframework.boot:spring-boot-starter-webflux')

	// Logging
	implementation 'org.springframework.boot:spring-boot-starter-logging'

	// Infrastructure
	implementation 'software.amazon.awssdk:s3:2.35.10'
	implementation ('com.azure:azure-storage-blob:12.32.0')
	implementation 'org.apache.qpid:qpid-jms-client:2.9.0'

	// Utils
	implementation 'org.apache.commons:commons-lang3:3.19.0'
	implementation 'javax.xml.soap:javax.xml.soap-api:1.4.0'
	implementation 'com.github.spullara.mustache.java:compiler:0.9.14'
	implementation 'org.apache.tika:tika-core:3.2.3'

	// Fhir
	implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-dstu3:7.6.1'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.assertj:assertj-core:3.27.6"
	testImplementation 'org.apache.commons:commons-compress:1.28.0'
	testImplementation(platform('org.testcontainers:testcontainers-bom:2.0.2'))
	testImplementation 'commons-io:commons-io:2.21.0'
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.awaitility:awaitility:4.3.0'
	testImplementation 'org.wiremock:wiremock-standalone:3.13.1'
	testImplementation 'com.squareup.okhttp3:okhttp:5.3.0'
	testImplementation 'com.squareup.okhttp3:mockwebserver3:5.3.0'
	testImplementation 'com.adobe.testing:s3mock-testcontainers:4.10.0'
	testImplementation "org.springframework.cloud:spring-cloud-gateway-server:4.3.2"

	spotbugs 'com.github.spotbugs:spotbugs:4.9.8'
	spotbugs 'com.github.spotbugs:spotbugs-annotations:4.9.8'

	pitest 'com.arcmutate:base:1.3.2'
	pitest 'com.arcmutate:pitest-git-plugin:2.1.0'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
	dependsOn test // tests are required to run before generating the report
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/intTest/java')
		}

		resources {
			srcDir file('src/intTest/resources')
		}
	}
}

tasks.register('interoperabilityTestingToolJsonToXml', JavaExec) {
	dependsOn 'classes'
	mainClass.set('uk.nhs.adaptors.gp2gp.transformjsontoxmltool.TransformJsonToXml')
	classpath = sourceSets.main.runtimeClasspath
}

configurations {
	integrationTestCompileOnly.extendsFrom testCompileOnly
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntime.extendsFrom testRuntimeOnly
	integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

tasks.register('integrationTest', Test) {
	// Runs integration tests
	description = 'Runs integration tests.'
	group = 'verification'

	// Enable JUnit 5
	useJUnitPlatform()

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test
}

tasks.withType(Test).configureEach {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

check.dependsOn integrationTest

spotbugsTest.enabled = false
spotbugsIntegrationTest.enabled = false
spotbugsMain {
	reports {
		html {
			enabled(true)
		}
		xml {
			enabled(true)
		}
	}
	excludeFilter.set(file("spotbugs/exclude.xml"))
}

pitest {
	pitestVersion = '1.16.3'
	junit5PluginVersion = '1.2.1'
	outputFormats = ['gitci']

	// git feature limits analysis to contents of PR only
	features = ["+GIT(from[HEAD~1])"]

	// PRs which don't introduce any production code changes, shouldn't fail the Mutation Testing Actions Workflow
	failWhenNoMutations = false

	mutators = ['STRONGER', 'EXTENDED_ALL']

	threads = project.getGradle().getStartParameter().getMaxWorkerCount()
}

pitestGithub {
	deleteOldSummaries = true
}

sonar {
	properties {
		property("sonar.host.url", "https://sonarcloud.io")
		property("sonar.projectKey", "NHSDigital_integration-adaptor-gp2gp")
		property("sonar.organization", "nhsdigital")
		property("sonar.exclusions", "**/transformjsontoxmltool/*")
	}
}

bootJar {
	exclude("**/TransformJsonToXml*")
}
